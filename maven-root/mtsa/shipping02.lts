// This model contains the shipping service with the CONTROLLER.
// The controller interface is specified.
// The pre and post conditions of the boxes are not described
// As expected, without the specification of the pre and post conditions the requirements are not satisfied

//***************************************************************************************************
// ENVIRONMENT
//***************************************************************************************************

set User = {userReq, offerRcvd, usrAck, usrNack, respOk, reqCancelled}
set Producer = {prodInfoReq, infoRcvd, prodReq, prodCancel}
set Shipper = {shipInfoReq, costAndTime, shipReq, shipCancel}
set A = {User, Producer, Shipper}


PRODUCER 	= 						(	prodInfoReq	->	REQUESTED		), 
					REQUESTED 	  = (	infoRcvd	->	ORDER_PENDING	),
					ORDER_PENDING = (	prodReq		->	PRODUCER 
										| 
										prodCancel	->	PRODUCER		).

 
SHIPPER 	= 						   (	shipInfoReq	->	REQUESTED		), 
					REQUESTED	 	 = (	costAndTime	->	SHIPPING_PENDING),
					SHIPPING_PENDING = (	shipReq		->	SHIPPER 
											| 
											shipCancel	->	SHIPPER			).


USER = 									(	userReq		->	REQUESTED		), 
					REQUESTED 		  = (	offerRcvd	->	ACK_NACK		),
					ACK_NACK 		  = (	usrAck		->	ACKD 
											| 
											usrNack		-> NAKD), 
					ACKD 			  = (	respOk		->	USER			),
					NAKD			  =( reqCancelled		->	USER	).


||Environment = (USER || SHIPPER || PRODUCER).


 
//***************************************************************************************************
// COMPONENTS
//***************************************************************************************************
COMPONENT1 =	INIT,
				INIT 			= 	(	userReq -> 	MANAGEREQUEST 	),
				box MANAGEREQUEST 	= 	(	respOk 	-> 	INIT	
										//|
										//reqCancelled -> INIT		
									)[A\{userReq,respOk}].
									

set PreparingOfferInterface={prodInfoReq, infoRcvd, shipInfoReq, costAndTime}

set ManageRequestInterface={prodReq, shipReq}
set DeclineRequestInterface={prodCancel, shipCancel}

COMPONENT2 =	INIT,
				INIT 				= 	(	userReq 	-> 	PREPARINGOFFER 				),
				box PREPARINGOFFER	= 	(	offerRcvd 	->	WAITINGFORUSERCHOICE 		
										)[PreparingOfferInterface],	
				WAITINGFORUSERCHOICE=	(	usrAck		->	MANAGEREQUEST
											|
											usrNack		-> 	DECLINEREQUEST
										),
				box	MANAGEREQUEST	=	(	respOk 		-> 	INIT	
										)[ManageRequestInterface],
				box DECLINEREQUEST	=	( 	reqCancelled -> INIT	
										)[DeclineRequestInterface].



// experiment 1
|| PartialComponent1=COMPONENT1.

// experiment 2 (comment the post-conditions)
// experiment 3 (uncomment the post-conditions)
|| PartialComponent2=COMPONENT2.


|| System = (Environment || CONTROLLER).

//***************************************************************************************************
// REQUIREMENTS
//***************************************************************************************************
assert P1 = (! ( (!F_UsrReq) U (F_ShipInfoReq || F_ProdInfoReq) ) )

assert P2	=[](F_UsrReq->( ! ( (!F_InfoRcvd) U F_OfferRcvd)))

assert P3=[](F_UsrReq->(((!((!F_UserAck) W F_ShipReq))&&<>F_ShipReq) && ((!(!(F_UserAck) W F_ProdReq))&&<>F_ProdReq) ))
assert P3a =[]((F_UsrReq&& (<>F_RespOk))->((!((!F_UserAck) W F_ShipReq))&& (<>F_ShipReq)))

assert P3b=[]((F_UsrReq&& (<>F_RespOk))->((!((!F_UserAck) W F_ProdReq))&& (<>F_ProdReq)))

assert P4=[](F_UsrReq->(!((!(F_UserNack) W (F_ProdCancel || F_ShipCancelled)))))

assert P4a=[]((F_UsrReq&&<>F_ProdCancel)->(!((!(F_UserNack) W (F_ProdCancel)))))
assert P4b=[]((F_UsrReq&&<>F_ShipCancelled)->(!((!(F_UserNack) W (F_ShipCancelled)))))


assert P5=[]( F_UsrReq->(!((!(F_ProdCancelled) W F_ReqCancelled)) && !((!(F_ShipCancelled) W F_ReqCancelled))))
assert P5a=[]( F_UsrReq&&<>F_ReqCancelled->(!((!(F_ProdCancelled) W F_ReqCancelled))))
assert P5b=[]( F_UsrReq&&<>F_ReqCancelled->(!(!(F_ShipCancelled) W F_ReqCancelled)))


assert P6=[]( (F_UserAck-> <>F_RespOk) && (F_UserNack-> <>F_ReqCancelled) &&<>F_UserAck &&<>F_UserNack)
assert P6a=[]( (<>F_UserAck)-> (<>F_RespOk))
assert P6b=[]( (<>F_UserNack)-> (<>F_ReqCancelled))
assert P7= []( (<>F_ReqCancelled)->(<>F_UsrReq))



//***************************************************************************************************
// FLUENTS
//***************************************************************************************************


fluent F_RespOk = <respOk, A\{respOk}>	initially False

fluent F_ShipInfoReq = <shipInfoReq, A\{shipInfoReq}> initially False
fluent F_ProdInfoReq = <prodInfoReq, A\{prodInfoReq}> initially False
fluent F_UsrReq = <userReq, A\{userReq}> initially False
fluent F_OfferRcvd = <offerRcvd, A\{offerRcvd}> initially False

fluent F_CostAndTime = <costAndTime, A\{costAndTime}> initially False
fluent F_InfoRcvd = <infoRcvd, A\{infoRcvd}> initially False

fluent F_UserNack = <usrNack, A\{usrNack}> initially False
fluent F_UserAck = <usrAck, A\{usrAck}> initially False
fluent F_ProdCancel = <prodCancel, A\{prodCancel}>  initially False

fluent F_ShipCancelled = <shipCancel, A\{shipCancel}> initially False

fluent F_ShipReq = <shipReq, A\{shipReq}> initially False
fluent F_ProdReq = <prodReq, A\{prodReq}> initially False


fluent F_ProdCancelled = <prodCancel, A\{prodCancel}>  initially False
fluent F_ReqCancelled = <reqCancelled, A\{reqCancelled}> initially False
fluent F_ReqNacked = <usrNack,  A\{usrNack}> initially False
fluent F_ProdRequested = <prodReq, A\{prodReq}> initially False
fluent F_OfferAck = <offerRcvd, A\{offerRcvd}> initially False
fluent F_End = <end, A> initially False

// necessary to make P2 satisfied
ltl_postcondition COMPONENT2 PREPARINGOFFER PREPARINGOFFER_POST=(<>(F_InfoRcvd))

 																				
ltl_postcondition COMPONENT2 MANAGEREQUEST CORRECTLY_MANAGE_REQUEST_POST=// necessary to make P3a satisfied
																	(
																		(<>F_ShipReq)
																		// necessary to make P3b satisfied
																		&&
																		(<>F_ProdReq)
																	)

ltl_postcondition COMPONENT2 DECLINEREQUEST CORRECTLY_DECLINE_REQUEST=// necessary to make P5a satisfied
																		(
																		(<>F_ProdCancelled)
																		// necessary to make P5b satisfied
																	&&
																		(<>F_ShipCancelled)
																	 )

ltl_precondition COMPONENT2 PREPARINGOFFER PREPARINGOFFER_PRE=<>(F_UserReq && !(<>F_RespOk||<>F_ReqCanc))

ltl_precondition COMPONENT2 MANAGEREQUEST CORRECTLY_MANAGE_REQUEST_PRE=[](F_UserReq -> <>F_InfoRcvd)


ltl_precondition COMPONENT2 DECLINEREQUEST CORRECTLY_DECLINE_REQUEST_PRE=[](F_UserReq -> <>F_InfoRcvd)

//***************************************************************************************************
// ADDITIONAL CONSTANTS
//***************************************************************************************************
const False = 0
const True = 1
